#
#    while A : B applies A to B until it saturates, thus becoming idempotent
#
#    demo: while nat_sum : 1 2 3 4
#    demo: frontstrip | | : | | | | |
#    demo: while frontstrip | | : | | | | |
#
def while : {_loop A : _LOOP : B}
#
#   ker A is the kernel of A defined by A:X=().
#
#   demo: ker null : a b c
#   demo: ker pass : a b c
#   demo: ker {3=(count:B)} : a b c d
#
#def ker : {if (A:B):B}


#
#   apif A:B returns the atoms in b where (A:b) is true.
#
#   demo: apif {has bin:B} : a b (bin:c) d e 
#   demo: apif {B=5} : 1 2 3 4 5 6 5 6
#
def apif : ap {if (A:B):B} 
#
#   in A:B applies B to each A-atom contents
#
#   demo: (in bin : {B B}) : (bin:x) (bin:y)
#   demo: ap (in bin : {B B}) : (bin:x) (bin:y)
#
def in : {(put A) * B * (get A)}
#
#    next is typically for sequences
#
#    demo: next {nat_sum:1 (last:B)} : 1 2 3
#    demo: next {nat_sum:last 2:B} : 1 1 2 3
#
def next : {B (A:B)}
#
#   (pow | | | : B):X does B:B:B:X three times, etc.
#
#   demo: (pow | | | : {B x}) : 6
#   demo: (pow | | | | : next {nat_sum : back | | : B}) : 1 1
#
def pow : {prod (dup A : (:B))}
#
#   Apply argument specified map.
#
#   demo: map (bin a:11) (bin b:22) (bin c:45) : b
#   demo: ap map (bin a:11) (bin b:22) (bin c:45) : a b c
#
def map : aq {if ((arg:A)=B):right:A}
